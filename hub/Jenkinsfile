#!groovyâ€‹
def projectDir = 'hub'
def gradleCmd = "${projectDir}/gradlew -b ${projectDir}/build.gradle"
node {
    def idOfRunningApplication

    stage('Check environment') {
        if (!env.HOST) {
            throw new IllegalStateException("Environment variable HOST my be set")
        }
    }

    stage('Pull SCM') {
        git url: 'https://github.com/alexander-t/hsha.git'
    }

    stage('Compile') {
        writeFile file: "${projectDir}/src/main/resources/buildInfo.properties", text: 'time=' + sh(script: 'date', returnStdout: true)
        sh "${gradleCmd} clean compileJava"
    }

    stage('Unit test') {
        sh "${gradleCmd} test jacocoTestReport"
        step([$class: 'JUnitResultArchiver', testResults: '**/build/test-results/test/TEST-*.xml'])
    }

    stage('Integration test') {
        sh "${gradleCmd} integrationTest"
        step([$class: 'JUnitResultArchiver', testResults: '**/build/test-results/integrationTest/TEST-*.xml'])
    }

    stage('Create Docker container') {
        // Save container id to kill before creating a new container with the same tag
        idOfRunningApplication = sh(script: 'docker ps -q --filter="ancestor=tarlinder/hsha/hub:latest"', returnStdout: true).trim()

        sh "${gradleCmd} build"
        sh "cp ${projectDir}/build/libs/hub.jar ${projectDir}/docker"
        sh "docker build -t tarlinder/hsha/hub hub/docker"
    }

    stage('Stop application and perform Docker cleanup') {
        killContainersWithIds(idOfRunningApplication)
        runIgnoringFailure('docker rm $(docker ps -q -f status=exited)',
                "There are no containers to remove")

        runIgnoringFailure('docker rmi $(docker images -q --filter "dangling=true")',
                "There are no dangling images to remove")
    }

    stage('Start application') {
        sh "docker run -d -i -p 8090:8090 tarlinder/hsha/hub"
        retry(10) {
            sleep(5)
            sh "wget -O /dev/null -nv http://${env.HOST}:8090/"
        }
    }
}

def runIgnoringFailure(String command, String explanation) {
    try {
        sh command
    } catch (hudson.AbortException e) {
        println "Ignoring: ${e.message}"
        println "Probable explanation: " + explanation
    }
}

@NonCPS
def killContainersWithIds(String ids) {
    if (!"".equals(ids)) {
        for (String id : ids.split('\n')) {
            println "Killing container with id ${id}"
            sh "docker kill ${id}"
        }
    }
}